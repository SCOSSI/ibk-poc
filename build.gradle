buildscript {
	ext {
		springBootVersion = '1.5.6.RELEASE'
	}
	repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath ("com.moowork.gradle:gradle-node-plugin:1.2.0")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.moowork.node'
apply plugin: "com.moowork.gulp"
apply plugin: 'idea'

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-web')
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

node {
    // Version of node to use.
    version = '8.1.3'

    // Version of npm to use.
    npmVersion = '5.0.3'
}


task npmPackages(type: NpmTask) {
    description = "Install Node.js packages"
    args = [ 'install' ]
    inputs.files file('package.json')
    outputs.files file('node_modules')
}

task bowerInstall(type: NodeTask) {
    script = file('node_modules/bower/bin/bower')
    args = [ 'install']
    inputs.files file('bower.json')
    dependsOn npmPackages
}

// run npm install
gulp_default.dependsOn 'npmInstall'
// run npm install
gulp_default.dependsOn bowerInstall

// run gulp install
gulp_default.dependsOn 'installGulp'

// processResources is a Java task. Run the gulpfile.js before this task using the 'default' task in the gulpfile.js
processResources.dependsOn gulp_default

